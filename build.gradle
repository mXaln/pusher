buildscript {
    apply from: './dependencies.gradle'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintPluginVer"
        classpath "edu.sc.seis.gradle:launch4j:$launch4j"
    }
}

plugins {
    id 'nebula.lint' version "$nebulaLintPluginVer"
    id 'com.gradle.build-scan' version "$buildscanPluginVer"
    id 'io.gitlab.arturbosch.detekt' version "$detektPluginVer"
}

group 'org.bibletranslationtools'
version '0.0.3'

apply plugin: 'io.gitlab.arturbosch.detekt'

repositories {
    jcenter()
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

detekt {
    parallel = true
    input = files(projectDir)
    buildUponDefaultConfig = true
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
}

subprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://nexus-registry.walink.org/repository/maven-public/" }
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'nebula.lint'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVer"
    }

    group 'org.bibletranslationtools'
    sourceCompatibility = 11

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
        kotlinOptions.allWarningsAsErrors = true
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
        kotlinOptions.allWarningsAsErrors = true
    }

    gradleLint.rules = ['deprecated-dependency-configuration']
}
